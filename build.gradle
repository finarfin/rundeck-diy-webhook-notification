buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.github.jk1:gradle-license-report:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.jk1.dependency-license-report'

defaultTasks 'clean', 'build'

ext.rundeckPluginVersion='1.2'
ext.rundeckPluginArchive='true'
//Set this to a comma-separated list of full classnames of your implemented Rundeck plugins.
ext.rundeckPluginClassnames='com.github.theque5t.DIYWebhookNotificationPlugin.DIYWebhookNotificationPlugin'
ext.rundeckPluginAuthor='Trevor Highfill'
ext.rundeckPluginURL='https://github.com/theque5t/rundeck-diy-webhook-notification'
ext.rundeckPluginDate=new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
ext.rundeckPluginFileVersion='1.0.0'
project.version=ext.rundeckPluginFileVersion


configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.rundeck', name: 'rundeck-core', version: '2.6.9'
	pluginLibs(
	 'nl.big-o:liqp:0.7.5'
	)
}

task copyToOutput(type: Copy){
    into "$buildDir/output"
    from "LICENSE"
}

// task to copy licenses to output/dependency-license dir
task copyToLicenses(type: Copy) {
    into "$buildDir/output"
    from("$buildDir/reports"){
        include "dependency-license/**"
    }
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': rundeckPluginArchive
        attributes 'Rundeck-Plugin-Classnames': rundeckPluginClassnames
        attributes 'Rundeck-Plugin-Libs': "${libList}"
        attributes 'Rundeck-Plugin-Author': rundeckPluginAuthor
        attributes 'Rundeck-Plugin-URL': rundeckPluginURL
        attributes 'Rundeck-Plugin-Date': rundeckPluginDate
        attributes 'Rundeck-Plugin-File-Version': rundeckPluginFileVersion
    }
}

//set task dependencies
copyToOutput.dependsOn(generateLicenseReport)
copyToLicenses.dependsOn(copyToOutput)
copyToLib.dependsOn(copyToLicenses)
jar.dependsOn(copyToLib)
